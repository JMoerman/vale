#
# Main file for building the Vale tool
#

# Python imports
import os, os.path
import sys
import collections
# Scons imports
import SCons.Util

# Imported identifiers defined in the SConstruct file
Import('env')

####################################################################
#
#   Code to drive F#, FSLEX, and FSYACC tools
#
####################################################################

BUILD = '#tools/FsLexYacc/FsLexYacc.6.1.0/build'    # the '#' character makes this relative to the SConstruct file in the root of the repo
FSLEX = BUILD+'/fslex.exe'
FSYACC = BUILD+'/fsyacc.exe'
BIN = '#bin'

def fsyaccEmitter(target, source, env):
  # assume the initial target is a .fs file.  Add the .fsi target
  targetBase, targetExt = os.path.splitext(SCons.Util.to_String(target[0]))
  target.append(targetBase+'.fsi')
  return (target, source)

def add_fslexyacc(env):
  # probe for fslexyacc to ensure it is installed ahead of trying to use it to build
  fslexfile = File(FSLEX)
  if not os.path.isfile(str(fslexfile)) and not GetOption('clean'):
    packages_config = File('#tools/Vale/src/packages.config')
    packages_dir = Dir('#tools/FsLexYacc')
    if Execute('%s restore %s -PackagesDirectory %s' % (env['NUGET'], packages_config, packages_dir)):
      print("Unable to run nuget in order to restore FsLexYacc.  See INSTALL.md for more details.")
      Exit(1)
    #raise EnvironmentError('FsLexYacc missing.  Please install to: "%s"'%str(fslexfile))
    
  env['FSLEX'] = fslexfile
  fslex = Builder(action='$MONO $FSLEX $SOURCE -o $TARGET',
                           suffix = '.fs',
                           src_suffix = '.fsl')
  env.Append(BUILDERS = {'FSLex' : fslex})
 
  env['FSYACC'] = File(FSYACC)
  fsyacc = Builder(action='$MONO $FSYACC --module "Parse" $SOURCE -o $TARGET',
                           suffix = '.fs',
                           src_suffix = '.fsy',
                           emitter = fsyaccEmitter)
  env.Append(BUILDERS = {'FSYacc' : fsyacc})

def build_vale(sources, refs):
  senv = env.Clone()
  # Wrap all paths in File() so they are appropriately adjusted to be relative to the SConstruct file
  senv['REFS'] = []
  for r in refs:
    senv.Append(REFS = '-r')
    senv.Append(REFS = File(r))

  if sys.platform != 'win32':
    senv['FSC']='fsharpc'
  else:
    # probe for the F# compiler on the PATH
    fsc = FindFile('fsc.exe', os.environ['PATH'].split(';'))
    if fsc == None:
      installdir = os.environ.get('FSHARPINSTALLDIR')
      if installdir == None:
        raise EnvironmentError('fsc,exe missing from path and FSHARPINSTALLDIR.')
      senv['FSC']=File(os.path.join(installdir, 'fsc.exe'))
    else:
      senv['FSC']=fsc
  vale_tool = senv.Command(BIN+'/vale.exe', sources,
    '$FSC -g --platform:anycpu --standalone --mlcompatibility -O $SOURCES -o $TARGET $REFS')

  # add dependencies to vale.exe
  for r in refs:
    Depends(vale_tool, r)

  return vale_tool

add_fslexyacc(env)

####################################################################
#
#   Describe the files needed to build Vale
#
####################################################################

SRC = [
  'src/ast.fs',
  'src/ast_util.fs',
  'src/parse_util.fs',
  '../../obj/Vale/parse.fs',
  '../../obj/Vale/lex.fs',
  'src/emit_vale_text.fs',
  'src/transform.fs',
  'src/emit_common_base.fs',
  'src/emit_common_quick_code.fs',
  'src/emit_common_lemmas.fs',
  'src/emit_common_quick_export.fs',
  'src/emit_common_refine.fs',
  'src/emit_common_top.fs',
  'src/emit_fstar_text.fs',
  'src/main.fs'
  ]

if sys.platform == 'win32':
  # The default search path for env.Detect() is just windows\system32.
  # Search along the OS PATH, then quote the name to avoid spaces-in-path issues.
  csc = env.WhereIs('csc.exe', path=os.environ['PATH'])
  if csc == None:
    raise EnvironmentError('csc.exe missing from path.')
  env['CSC'] = '\"' + csc + '\"'
else: # mono
  env['CSC'] = env.Detect('dmcs')


REFS = ['../../tools/FsLexYacc/FsLexYacc.Runtime.6.1.0/lib/net40/FsLexYacc.Runtime.dll']

# build vale.exe
env.FSLex(target='../../obj/Vale/lex.fs', source='src/lex.fsl')
env.FSYacc(target='../../obj/Vale/parse.fs', source='src/parse.fsy')
vale_tool = build_vale(SRC, REFS)

# copy the dependencies to the BIN directory, too
dependencies = vale_tool
  
# Return a list containing the vale.exe tool and its dependent DLLs, for
# other scripts to use as additional dependencies.
# Also return the path to z3 relative to the root Sconstruct file.
vale_tool_results = dependencies
Return('vale_tool_results')
